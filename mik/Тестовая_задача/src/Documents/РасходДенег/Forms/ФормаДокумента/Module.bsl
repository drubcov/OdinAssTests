
&НаСервере
Процедура ЗаполнитьНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВзаиморасчетыСКомитентамиОстатки.Контрагент КАК Контрагент,
	|	ВзаиморасчетыСКомитентамиОстатки.Договор КАК Договор,
	|	ВзаиморасчетыСКомитентамиОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСКомитентами.Остатки(&ДатаДокумента, Организация = &Организация) КАК ВзаиморасчетыСКомитентамиОстатки
	|ГДЕ
	|	ВзаиморасчетыСКомитентамиОстатки.СуммаОстаток > 0";    
	
	Запрос.УстановитьПараметр("Организация",Объект.Организация);
	
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Момент =  Новый Граница(Объект.Ссылка.МоментВремени(), ВидГраницы.Исключая); // Существующие движения документа не нужно учитывать
	Иначе 
		Момент = ТекущаяДатаСеанса();
	КонецЕсли;	
	Запрос.УстановитьПараметр("ДатаДокумента", Момент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокаТЧ = Объект.Оплаты.Добавить();
		СтрокаТЧ.Контрагент = ВыборкаДетальныеЗаписи.Контрагент;
		СтрокаТЧ.Договор = ВыборкаДетальныеЗаписи.Договор;
		СтрокаТЧ.Сумма = ВыборкаДетальныеЗаписи.СуммаОстаток;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Организация не выбрана!";
		Сообщение.Поле = "Объект.Организация";  
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	Если  Объект.Оплаты.Количество() > 0  Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект), "Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе 
		ЗаполнитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Оплаты.Очистить();
		ЗаполнитьНаСервере();
	КонецЕсли;

КонецПроцедуры

